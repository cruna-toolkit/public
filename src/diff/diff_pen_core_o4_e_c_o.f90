module diff_pen_core_o4_e_c_o

  use parameter

  private

  public :: diff_i1, diff_i2, diff_i3

  character(len=max_length_parameter), parameter, public :: diff_type = 'open'
  character(len=max_length_parameter), parameter, public :: diff_name = 'diff_pen_core_o4_e_c_o'

contains

!!!=================================================================================================
  subroutine diff_i1(q,dx)
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
    real(kind=rk), dimension(:,:), intent(inout)      :: q
    real(kind=rk)                , intent(in)         :: dx
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

    call diff_main(q,dx)

  end subroutine diff_i1
!!!==================================================================================================

!!!=================================================================================================
  subroutine diff_i2(q,dx)
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
    real(kind=rk), dimension(:,:), intent(inout)       :: q
    real(kind=rk)                , intent(in)          :: dx
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

    call diff_main(q,dx)

  end subroutine diff_i2
!!!==================================================================================================

!!!=================================================================================================
  subroutine diff_i3(q,dx)
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
    real(kind=rk), dimension(:,:), intent(inout)       :: q
    real(kind=rk)                , intent(in)          :: dx
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

    call diff_main(q,dx)

  end subroutine diff_i3
!!!==================================================================================================


!!!=================================================================================================
  subroutine diff_main(q,dx)
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!! 4th order explicit, stencil from PIVR code
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
    real(kind=rk) , dimension(:,:), intent(inout)       :: q
    real(kind=rk)                 , intent(in)          :: dx
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
    real(kind=rk) , dimension(:,:), allocatable         :: q_tmp
    integer                                             :: n1,n2
    integer                                             :: i,j

    n1 = size(q,1)
    n2 = size(q,2)

    allocate(q_tmp(n1,n2))

    ! compute A*U
    q_tmp(1   ,:) = -25.0_rk*q(1,:) +48.0_rk*q(2,:) -36.0_rk*q(3,:) +16.0_rk*q(4,:) -3.0_rk*q(5,:)
    q_tmp(2   ,:) =  -3.0_rk*q(1,:) -10.0_rk*q(2,:) +18.0_rk*q(3,:) - 6.0_rk*q(4,:) +1.0_rk*q(5,:)
    do j = 1,n2
       do i = 3,(n1-2)
          q_tmp(i,j) = 8.0_rk*(q(i+1,j) - q(i-1,j)) - 1.0_rk*(q(i+2,j) - q(i-2,j))
       end do
    end do
    q_tmp(n1-1,:) = -1.0_rk*q(n1-4,:) + 6.0_rk*q(n1-3,:) -18.0_rk*q(n1-2,:) +10.0_rk*q(n1-1,:) + 3.0_rk*q(n1,:)
    q_tmp(n1  ,:) =  3.0_rk*q(n1-4,:) -16.0_rk*q(n1-3,:) +36.0_rk*q(n1-2,:) -48.0_rk*q(n1-1,:) +25.0_rk*q(n1,:)

    ! dx factor
    q = q_tmp/(dx*12.0_rk)

  end subroutine diff_main
!!!==================================================================================================

end module diff_pen_core_o4_e_c_o
