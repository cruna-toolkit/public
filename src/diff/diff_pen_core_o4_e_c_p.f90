module diff_pen_core_o4_e_c_p

  use parameter

  private

  public :: diff_i1, diff_i2, diff_i3

  character(len=max_length_parameter), parameter, public :: diff_type = 'periodic'
  character(len=max_length_parameter), parameter, public :: diff_name = 'diff_pen_core_o4_e_c_p'

contains

!!!=================================================================================================
  subroutine diff_i1(q,dx)
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
    real(kind=rk), dimension(:,:), intent(inout)      :: q
    real(kind=rk)                , intent(in)         :: dx
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

    call diff_main(q,dx)

  end subroutine diff_i1
!!!==================================================================================================

!!!=================================================================================================
  subroutine diff_i2(q,dx)
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
    real(kind=rk), dimension(:,:), intent(inout)       :: q
    real(kind=rk)                , intent(in)          :: dx
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

    call diff_main(q,dx)

  end subroutine diff_i2
!!!==================================================================================================

!!!=================================================================================================
  subroutine diff_i3(q,dx)
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
    real(kind=rk), dimension(:,:), intent(inout)       :: q
    real(kind=rk)                , intent(in)          :: dx
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

    call diff_main(q,dx)

  end subroutine diff_i3
!!!==================================================================================================


!!!=================================================================================================
  subroutine diff_main(q,dx)
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!! 4th order explicit, stencil from PIVR code
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
    real(kind=rk) , dimension(:,:), intent(inout)       :: q
    real(kind=rk)                 , intent(in)          :: dx
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
    real(kind=rk) , dimension(:,:), allocatable         :: q_tmp
    integer                                             :: n1,n2
    integer                                             :: i,j

    n1 = size(q,1)
    n2 = size(q,2)

    allocate(q_tmp(n1,n2))

    ! compute A*U
    q_tmp(1   ,:) = 8.0_rk*(q(2,:) - q(n1,:)) - 1.0_rk*(q(3,:) - q(n1-1,:))
    q_tmp(2   ,:) = 8.0_rk*(q(3,:) - q( 1,:)) - 1.0_rk*(q(4,:) - q(n1  ,:))
    do j = 1,n2
       do i = 3,(n1-2)
          q_tmp(i,j) = 8.0_rk*(q(i+1,j) - q(i-1,j)) - 1.0_rk*(q(i+2,j) - q(i-2,j))
       end do
    end do
    q_tmp(n1-1,:) = 8.0_rk*(q(n1,:) - q(n1-2,:)) - 1.0_rk*(q(1,:) - q(n1-3,:))
    q_tmp(n1  ,:) = 8.0_rk*(q( 1,:) - q(n1-1,:)) - 1.0_rk*(q(2,:) - q(n1-2,:))

    ! dx factor
    q = q_tmp/(dx*12.0_rk)

  end subroutine diff_main
!!!==================================================================================================

end module diff_pen_core_o4_e_c_p
